apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'sign'
apply plugin: 'eclipse'

group = 'com.trigonic'
artifact = 'gradle-rpm-plugin'
version = '0.8'

def props = new Properties();
def localProperties = new File("local.properties")
if (localProperties.exists()) localProperties.withInputStream { props.load(it) }

configurations {
    deployerJars
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'de.huxhorn.gradle:de.huxhorn.gradle.pgp-plugin:0.0.3'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'org.redline-rpm:redline:1.1.9'

    groovy 'org.codehaus.groovy:groovy:1.7.10'

    testCompile 'junit:junit:4.+'

    testRuntime 'org.slf4j:slf4j-simple:1.6.1'

    deployerJars 'org.apache.maven.wagon:wagon-http:1.0-beta-7'
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives packageJavadoc
    archives packageSources
}

pgp {
    secretKeyRingFile = new File("${System.properties['user.home']}/.gnupg/secring.gpg")
    keyId = props.get('sign.key.id')
    password = props.get('sign.key.password')
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
            authentication(userName: props.get('maven.repository.user'), password: props.get('maven.repository.password'))
        }

        pom.project {
            name 'gradle-rpm-plugin - Gradle plugin for constructing RPM packages'
            packaging 'jar'
            description 'Provides a task similar to Tar and Zip for constructing RPM package files.'
            url 'https://github.com/AlanKrueger/gradle-rpm-plugin'
            inceptionYear '2011'

            parent {
                groupId 'org.sonatype.oss'
                artifactId 'oss-parent'
                version '7'
            }
     
            scm {
                url 'scm:git://github.com/AlanKrueger/gradle-rpm-plugin.git'
                connection 'scm:git://github.com/AlanKrueger/gradle-rpm-plugin.git'
            }
     
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
     
            developers {
                developer {
                    id 'akrueger'
                    name 'Alan Krueger'
                    email 'alan@trigonic.com'
                    roles{
                        role 'Developer'
                    }
                    timezone '-6'
                }
            }
        }
    }
}

task createWrapper(type: Wrapper) {
    gradleVersion = '1.0-milestone-3'
}

