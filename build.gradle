apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'nexus'
apply plugin: 'idea'

group = 'com.trigonic'
version = '1.4-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'org.redline-rpm:redline:1.1.10+'

    groovy 'org.codehaus.groovy:groovy:1.8.+'

    testCompile 'junit:junit:4.+'
    testCompile([group: 'org.gmock', name: 'gmock', version: '0.8.2', transitive: false])
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives packageJavadoc
    archives packageSources
}

modifyPom {
    project {
        name 'gradle-rpm-plugin - Gradle plugin for constructing RPM packages'
        packaging 'jar'
        description 'Provides a task similar to Tar and Zip for constructing RPM package files.'
        url 'https://github.com/TrigonicSolutions/gradle-rpm-plugin'
        inceptionYear '2011'

        parent {
            groupId 'org.sonatype.oss'
            artifactId 'oss-parent'
            version '7'
        }
 
        scm {
            url 'scm:git://github.com/TrigonicSolutions/gradle-rpm-plugin.git'
            connection 'scm:git://github.com/TrigonicSolutions/gradle-rpm-plugin.git'
        }
 
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
 
        developers {
            developer {
                id 'akrueger'
                name 'Alan Krueger'
                email 'alan@trigonic.com'
                roles{
                    role 'Developer'
                }
                timezone '-6'
            }
        }
    }
}

task createWrapper(type: Wrapper) {
    gradleVersion = '1.2'
}

