apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'

group = 'com.trigonic'
description 'Provides a task similar to Tar and Zip for constructing RPM package files.'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'org.apache.commons:commons-lang3:3.1'
    // Do NOT use a dynamic version like 1.1+, since it'll show up exactly like that in the pom, and 1.1+ is a proper maven version
    compile 'org.redline-rpm:redline:1.1.10'
    compile('org.vafer:jdeb:1.0.1') {
        // Individual listing so that they translate into the pom file
        exclude group: 'org.apache.maven', module: 'maven-core'
        exclude group: 'org.apache.maven', module: 'maven-plugin-api'
        exclude group: 'org.apache.maven', module: 'maven-project'
        exclude group: 'org.apache.maven', module: 'maven-artifact'
        exclude group: 'org.codehaus.plexus', module: 'plexus-utils'
    }

    compile localGroovy()

    testCompile 'junit:junit:4.11'
    testCompile([group: 'org.gmock', name: 'gmock', version: '0.8.2', transitive: false])
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives packageJavadoc
    archives packageSources
}

pom {
    project {
        name 'gradle-rpm-plugin - Gradle plugin for constructing RPM packages'
        packaging 'jar'
        description 'Provides a task similar to Tar and Zip for constructing RPM package files.'
        url 'https://github.com/TrigonicSolutions/gradle-rpm-plugin'
        inceptionYear '2011'

        parent {
            groupId 'org.sonatype.oss'
            artifactId 'oss-parent'
            version '7'
        }
 
        scm {
            url 'scm:git://github.com/TrigonicSolutions/gradle-rpm-plugin.git'
            connection 'scm:git://github.com/TrigonicSolutions/gradle-rpm-plugin.git'
        }
 
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
 
        developers {
            developer {
                id 'akrueger'
                name 'Alan Krueger'
                email 'alan@trigonic.com'
                roles{
                    role 'Developer'
                }
                timezone '-6'
            }
            developer {
                id 'quidryan'
                name 'Justin Ryan'
                email 'justin@halfempty.org'
                roles{
                    role 'Developer'
                }
                timezone '-8'
            }
        }
    }
}

test {
  testLogging {
    exceptionFormat "full"
  }
}

task createWrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

